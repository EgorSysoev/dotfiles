# ZSH CONFIGURATION #
#####################

# Turn on Vi mode
bindkey -v

# Alias hub to git
eval "$(hub alias -s)"

# Allow rbenv if it's installed (which it should be!)
if which rbenv > /dev/null; then eval "$(rbenv init -)"; fi

# Source Tmuxinator if installed
[[ -s $HOME/.tmuxinator/scripts/tmuxinator ]] && source $HOME/.tmuxinator/scripts/tmuxinator

# Virtualenv & Virtualenvwrapper setup if installed
if which virtualenv > /dev/null;
then
  VIRTUALENVWRAPPER_PYTHON=/usr/local/bin/python
  export WORKON_HOME=$HOME/.virtualenvs
  source /usr/local/share/python/virtualenvwrapper.sh
  export PIP_VIRTUALENV_BASE=$WORKON_HOME
fi

# Load completions for Ruby, Git, etc.
autoload compinit && compinit -C

# Case insensitive auto-complete
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

# PROMPT FUNCTIONS AND SETTINGS #
#################################

# Colors
autoload -U colors && colors
setopt prompt_subst

# Display Virtualenv cleanly in right column
function virtualenv_info {
  [ $VIRTUAL_ENV ] && echo '('`basename $VIRTUAL_ENV`')'
}

# Display Rbenv cleanly if Rbenv is installed
function rbenv_info {
  if which rbenv > /dev/null; then echo "$(rbenv version-name)"; fi
}

# Command Status
local command_status="%(?,%{$fg[green]%}✔%{$reset_color%},%{$fg[red]%}✘%{$reset_colors%})"

# Vi Mode Statue
function zle-keymap-select {
  VIMODE="${${KEYMAP/vicmd/command}/(main|viins)/insert}"
  zle reset-prompt
}
zle -N zle-keymap-select

# Show relative path on one line, then command status
PROMPT='
%{$fg[cyan]%}%n %{$fg[white]%}: %{$fg[cyan]%}%~ %{$fg[white]%}: %{$fg[cyan]%}${VIMODE}
${command_status} %{$reset_color%} '

# Show virtualenv, rbenv, branch, sha, and repo dirty status on right side
RPROMPT='%{$fg[cyan]%}$(virtualenv_info)%{$fg[white]%}$(rbenv_info)$(~/bin/git-cwd-info.sh)%{$reset_colors%}'
